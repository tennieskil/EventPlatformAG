Window CategoryWindow {
    Category entity
    Label Category_L {
        String text := ['Category: ' + $CategoryWindow.entity$.name]
    }
    Label Title_L {
        String text := ['Events:']
    }
    Table Table_T {
        Set(Event) rows := [$CategoryWindow.entity$.events]
        Event selected := [null]
        columns {
            ['Title'] : Label title {
                event onView (text) {
                    try {
                        text := [$Table_T.row$.title]
                    } catch (SecurityException) {
                        text := ['RESTRICTED']
                    }
                }
            }
            ['Date'] : Label date {
                event onView (text) {
                    try {
                        text := [$Table_T.row$.date.toString()]
                    } catch (SecurityException) {
                        text := ['RESTRICTED']
                    }
                }
            }
            ['Location'] : Label loc {
                event onView (text) {
                    try {
                        text := [$Table_T.row$.location]
                    } catch (SecurityException) {
                        text := ['RESTRICTED']
                    }
                }
            }
            ['Owner'] : Label own {
                event onView (text) {
                    try {
                        text := [$Table_T.row$.owner.name.concat(' ').concat($Table_T.row$.owner.surname)]
                    } catch (SecurityException) {
                        text := ['RESTRICTED']
                    }
                }
            }
        }
    }
    Button View_Evt_B {
        String text:= ['View Event']
        event onClick {
            if [(not $Table_T.selected$.oclIsUndefined())] {
                try {
                    if [$CategoryWindow.caller$.manages->includes($Table_T.selected$)] {
                        open EventManagerWindow(caller:[$CategoryWindow.caller$], role:[$CategoryWindow.caller$.role], entity:[$Table_T.selected$])
                    } else {
                        open EventUserWindow(caller:[$CategoryWindow.caller$], role:[$CategoryWindow.caller$.role], entity:[$Table_T.selected$])
                    }

                } catch (SecurityException) {
                    notification(['Error'],['Not allowed'],[500])
                }
            } else {
                notification(['Error'],['Nothing selected'],[500])
            }
        }
    }
    Button Remove_B {
        String text:= ['Remove Event from Category']
        event onClick {
            if [(not $Table_T.selected$.oclIsUndefined())] {
                try {
                    [$Table_T.selected$.categories] -= [$CategoryWindow.entity$]

                    notification(['Success'],['Category removed'],[500])
                    Table_T.rows := [null]
                    reevaluate Table_T.rows
                } catch (SecurityException) {
                    notification(['Error'],['Not allowed'],[500])
                }
            } else {
                notification(['Error'],['Nothing selected'],[500])
            }
        }
    }
    Button Back_B {
        String text := ['Back']
        event onClick {
            back
        }
    }
}
