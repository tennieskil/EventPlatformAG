Window EventManagerWindow {
    Event entity

    Label Title_L {
        String text := ['Title']
    }
    TextField Title_TF {
        String text := [$EventManagerWindow.entity$.title]
    }
    Label Description_L {
        String text := ['Description']
    }
    TextField Description_TF {
        String text := [$EventManagerWindow.entity$.description]
    }
    Label Location_L {
        String text := ['Location']
    }
    TextField Location_TF {
        String text := [$EventManagerWindow.entity$.location]
    }
    BooleanField Private_BF {
        Boolean value := [$EventManagerWindow.entity$.private]
        String text := ['Private event']
    }
    Label Date_L {
        String text := ['Date']
    }
    DateField Date_DF {
        LocalDateTime value := [$EventManagerWindow.entity$.date.atStartOfDay()]
    }
    Label Category_L {
        String text := ['Category']
    }
    Table Table_Category_T {
        Set(Category) rows := [Category.allInstances()]
        Set(Category) selected  := [$EventManagerWindow.entity$.categories]
        columns {
            ['Name'] : Label name {
                event onView (text) {
                    try {
                        text := [$Table_Category_T.row$.name]
                    } catch (SecurityException) {
                        text := ['RESTRICTED']
                    }
                }
           }
        }
    }
    Button Update_B {
        String text := ['Update']
        event onClick {
            if [(not $Title_TF.text$.oclIsUndefined()) and
                (not ($Title_TF.text$.size()=0)) and
                (not $Description_TF.text$.oclIsUndefined()) and
                (not ($Description_TF.text$.size()=0)) and
                (not $Date_DF.value$.oclIsUndefined()) and
                (not $Location_TF.text$->oclIsUndefined()) and
                (not ($Location_TF.text$.size()=0)) and
                (not $Table_Category_T.selected$->oclIsUndefined())  and
                (not ($Table_Category_T.selected$->size() < 1))
            ] {
                try {
                    [$EventManagerWindow.entity$.title] := [$Title_TF.text$]
                    [$EventManagerWindow.entity$.private] := [$Private_BF.value$]
                    [$EventManagerWindow.entity$.description] := [$Description_TF.text$]
                    [$EventManagerWindow.entity$.location] := [$Location_TF.text$]
                    [$EventManagerWindow.entity$.date] := [$Date_DF.value$.toLocalDate().plusDays(1)]

                    foreach cat1 in [$EventManagerWindow.entity$.categories] {
                        [$EventManagerWindow.entity$.categories]-=[$cat1$]
                    }
                    foreach cat2 in [$Table_Category_T.selected$] {
                        [$EventManagerWindow.entity$.categories]+=[$cat2$]
                    }

                    notification(['Success'],['Event updated'],[500])
                } catch (SecurityException) {
                    notification(['Error'],['Not allowed'],[500])
                }
            } else {
                notification(['Error'],['Form contains errors.'],[500])
            }
        }
    }

    Button Manage_B {
        String text:= ['Manage Event']
        Set(Person) probe
        event onClick {
            if [$EventManagerWindow.caller$.manages->includes($EventManagerWindow.entity$)] {
                open ManageEventWindow(caller:[$EventManagerWindow.caller$], role:[$EventManagerWindow.caller$.role], entity:[$EventManagerWindow.entity$])
            } else {
                notification(['Error'],['Not allowed'],[500])
            }
        }
    }

    Button Back_B {
        String text := ['Back']
        event onClick {
            back
        }
    }
}
