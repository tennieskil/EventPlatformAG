Window LoginWindow {
    

    String text := ['Log In']

    Label Name_L {
        String text := ['Login']
    }
    TextField Name_TF {
        String text :=  [null] 
    }
    Label Password_L {
        String text := ['Passw']
    }
    PasswordField Password_TF {
        String text :=  [null] 
    }

    Button Register_B {
        String text := ['Register']

        event onClick {
            if [$Name_TF.text$.oclIsUndefined()] {
                Name_TF.error := ['Error']
            } else {
                Name_TF.error := [null]
            }
            if [$Password_TF.text$.oclIsUndefined()] {
                Password_TF.error := ['Error']
            } else {
                Password_TF.error := [null]
            }
            if [$Name_TF.error$ = null and $Password_TF.error$ = null] {           
                if[Person.allInstances()->forAll(c|c.login <> $Name_TF.text$)] {
                    newUser := new Person
                    [$newUser$.login] := [$Name_TF.text$]
                    [$newUser$.password] := [$Password_TF.text$]
                    [$newUser$.personalRole] := [Role::USER] 
                    open MainWindow(caller:[$newUser$], role:[$newUser$.personalRole])
                 } else {
                    notification(['Error'],['This login name already exists. Choose another one.'],[500]) 
                    Name_TF.text :=  [null]  
                    Password_TF.text :=  [null] 
                 }   
            }
            else {
                notification(['Error'],['The form contains errors. Please, check the form.'],[500]) 
            }
        }
    }

    Button Login_B {
        String text := ['Log in']
        Person loggedUser
        event onClick {
                loggedUser := [Person.allInstances()->any(c|c.login = $Name_TF.text$ and c.password = $Password_TF.text$)]

            if [not($loggedUser$.oclIsUndefined())] {
                    open MainWindow(caller:[$loggedUser$], role:[$loggedUser$.personalRole])
            }
            else {
                notification (['Message'],['Wrong credentials.'],[500])
                Name_TF.text :=  [null]  
                Password_TF.text :=  [null] 
            }
        }
    }

    
}